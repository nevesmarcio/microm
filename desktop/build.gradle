//apply plugin: "java"

sourceCompatibility = 1.8
[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

project.ext.mainClassName = "pt.me.microm.Main"
project.ext.assetsDir = new File("../assets");
task run(dependsOn: classes, type: JavaExec) {
    main = project.mainClassName
    classpath = sourceSets.main.runtimeClasspath
    standardInput = System.in
    workingDir = project.assetsDir
    ignoreExitValue = true

    jvmArgs = ['-Xms32m',
               '-Xmx64m',
               '-Xdebug',
               '-Xrunjdwp:transport=dt_socket,address=9999,server=y,suspend=n', // suspend=y waits for debugger to attach before starting execurtion
               '-XstartOnFirstThread' // https://www.badlogicgames.com/wordpress/?p=3870 On Mac OS X, you need to specify “-XstartOnFirstThread” as a VM argument when running your app. GLFW requires this. It’s unlikely this can be fixed in code.
    ]
}
task dist(type: Jar) {
    from files(sourceSets.main.output.classesDirs)
    from files(sourceSets.main.output.resourcesDir)
    from { configurations.compile.collect { zipTree(it) } }
    from files(project.assetsDir);

    manifest {
        attributes 'Main-Class': project.mainClassName
    }
}
dist.dependsOn classes

dependencies {
    //logback dependencies
    compile group: 'ch.qos.logback', name: 'logback-classic', version: '1.2.3'
    compile group: 'ch.qos.logback', name: 'logback-core', version: '1.2.3'

    // desktop specific local libs
    compile fileTree(dir: 'libs', include: '*.jar')
}

//create a single Jar with all dependencies: run it - java -jar hello-all-1.0.jar
task fatJar(type: Jar) {
    manifest {
        attributes 'Implementation-Title': 'Gradle Jar File Example',
                'Implementation-Version': version,
                'Main-Class': project.mainClassName
    }
    baseName = project.name + '-all'
    from files(sourceSets.main.output.classesDirs)
    from files(sourceSets.main.output.resourcesDir)
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    from files(project.assetsDir);
    with jar
}